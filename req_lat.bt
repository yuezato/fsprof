#!/usr/local/bin/bpftrace

#include <linux/fs.h>
#include <linux/uio.h>
#include <linux/bio.h>
#include <linux/genhd.h>
#include <linux/blkdev.h>
#include <scsi/scsi_device.h>
#include <scsi/scsi_cmnd.h>
#include <linux/libata.h>
#include <linux/device.h>
#include <linux/buffer_head.h>
#include <linux/huge_mm.h>
#include <linux/fs.h>
#include <linux/iomap.h>

kprobe:blk_mq_start_request
/ ((struct request*)arg0)->bio->bi_disk->disk_name == "sdb" /
{
 $req = (struct request*)arg0;
 @mq_start_req[$req] = nsecs;
}

kprobe:blk_mq_complete_request
/ @mq_start_req[(struct request*)arg0] /
{
 $req = (struct request*)arg0;
 $sec = $req->__sector;

 $elapsed_ns = nsecs - @mq_start_req[$req];
 $elapsed_us = $elapsed_ns / 1000;
 $elapsed_ms = $elapsed_us / 1000;
 @mq_time_millisecs = lhist( $elapsed_ms, 0, 30, 2 );
 @mq_assoc[$req->__data_len, $sec % 8] = stats($elapsed_ms);

 @mq_sectors = hist($req->__sector);
 @mq_start_req[$req] = 0;

 printf("sec: %u, size: %u, segs: %u", $sec, $req->__data_len, $req->nr_phys_segments);
 printf(", time:%u.%u[ms]", $elapsed_us / 1000, $elapsed_us % 1000);
 printf("\n");

 @mq_start_req[$req] = 0;
}

END
{
 clear(@mq_start_req);
}